#!raku

use Sparky::JobApi;

class Pipeline does Sparky::JobApi::Role {

  has $.rakudo-install-dir = "{%*ENV<HOME>}/projects/rakudo";

  has $.r3-install-dir = "{%*ENV<HOME>}/projects/r3";

  method stage-main {

    my $q = self.new-job;

    $q.queue(%(
      description => "install rakudo",
      tags => %(
        stage => "install-rakudo"
      )
    ));

    my $s = self.wait-job($q);

    die if $s<FAIL>;

    $q = self.new-job;

    $q.queue(%(
      description => "run r3",
      tags => %(
        stage => "run-r3"
      )
    ));

    $s = self.wait-job($q);

    die if $s<FAIL>;

  }

  method stage-install-rakudo {

    directory $.rakudo-install-dir;

    git-scm "https://github.com/rakudo/rakudo.git", %(
      to => $.rakudo-install-dir
    );

    bash "perl Configure.pl --gen-moar --gen-nqp --backends=moar", %(
      description => "configure",
      cwd => $.rakudo-install-dir,
    );

    bash "make install", %(
      description => "make install",
      cwd => $.rakudo-install-dir,
    );

  }
  

   method stage-run-r3 {

    directory $.r3-install-dir;

    git-scm "https://github.com/melezhik/r3tool.git", %(
      to => $.r3-install-dir
    );

    bash "tomty --all --env=source --show-failed --only=closed", %(
      description => "r3 test [closed]",
      cwd => $.r3-install-dir,
      envvars => %(
        RAKUBIN => "{$.rakudo-install-dir}/install/bin/"
      )
    );      
   } 
}

Pipeline.new.run;
